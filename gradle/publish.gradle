/*
 * Copyright 2016 Palantir Technologies, Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    destinationDir rootProject.file("${buildDir}/libs")
}

tasks.build.dependsOn sourceJar, javadocJar

publishing {
    publications {
        bintray(MavenPublication) {
            from components.java
            artifact(sourceJar)
            artifact(javadocJar)
            version project.version.replaceAll(/-.*/, '-SNAPSHOT')
        }
    }
}

bintray {
    user = System.env.BINTRAY_USERNAME
    key = System.env.BINTRAY_PASSWORD
    publish = true
    pkg {
        repo = 'releases'
        name = 'dropwizard-web-logger'
        userOrg = 'palantir'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/palantir/dropwizard-web-logger.git'
        publications = ['bintray']
    }
}

bintrayUpload.onlyIf {
    System.env.BINTRAY_USERNAME && System.env.BINTRAY_PASSWORD && project.version ==~ /\d+\.\d+\.\d+/
}

bintrayUpload.dependsOn 'generatePomFileForBintrayPublication', 'build'
